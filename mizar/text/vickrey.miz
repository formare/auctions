:: Vickrey's theorem on efficiency and equilibrium in second price auctions
::  by Marco B. Caminati
:: February 2013
:: Copyright (c) 2013 by Marco B. Caminati
:: Dually licensed under
:: * Creative Commons Attribution (CC-BY) 3.0
:: * ISC License (1-clause BSD License)
:: See LICENSE file for details
:: Successfully and fully verified under 
:: MML ver. 5.8.1171 for Mizar ver. 8.1.01 (linux i386)

::{
environ

vocabularies TARSKI, XBOOLE_0, ZFMISC_1, SUBSET_1, NUMBERS, NAT_1, RELAT_1
, FUNCT_1, XCMPLX_0, ORDINAL1, XXREAL_0, FINSET_1, ARYTM_1 ,CARD_1, FUNCT_4
,FUNCOP_1 ,FOMODEL0 ,XBOOLEAN ,XXREAL_2 ,MEMBERED ,VICKREY ,MARGREL1, ORDINAL2;
 
constructors TARSKI
,NAT_1
,CARD_1
,ZFMISC_1
,NUMBERS
,RELSET_1 
,PBOOLE
,FINSET_1 
,ORDINAL1
,FUNCT_4
,RELAT_2
,FUNCOP_1
,XBOOLEAN
,RELAT_1 :::|
,XBOOLE_0
,XXREAL_2
,MEMBERED
,FOMODEL0 
,XREAL_0 :::to access XREAL_1:9
,MARGREL1
;

notations TARSKI :::notations directive is order-sensitive!
,XBOOLE_0
,FOMODEL0
,SUBSET_1
,NAT_1
,ZFMISC_1
,NUMBERS
,XCMPLX_0 :::for good typing of eg m+n is Nat
,RELAT_1 :::for dom
,FUNCT_1 :::for Function mode
,RELSET_1 :::for .: redef
,ORDINAL1
,CARD_1
,FINSET_1 
,FUNCT_4
,FUNCOP_1
,XXREAL_0 :::for attribute positive
,XBOOLEAN
,XXREAL_2
,MEMBERED
,MARGREL1 
;

registrations FOMODEL0
,ORDINAL1 
,XCMPLX_0 
,NAT_1 
,RELAT_1 :::dom R -> trivial
,NUMBERS
,FUNCT_1 :::dom f -> non trivial
,SUBSET_1 
,XBOOLE_0
,SETFAM_1 
,FINSET_1
,ZFMISC_1
,XREAL_0
,MEMBERED
,FUNCOP_1
,FUNCT_4 :::+* -> non-empty
,XBOOLEAN
,XXREAL_2
,YELLOW12 :::union -> empty
,XXREAL_3
,INT_2 :::non natural
,CARD_1 :::natural -> finite
;

equalities FOMODEL0 :::+*1
,FUNCOP_1 :::x.-->y 
,XBOOLEAN :::FALSE
,MARGREL1 :::BOOLEAN
,ORDINAL1 :::0
;

expansions FOMODEL0;

theorems TARSKI
,FUNCT_1
,RELAT_1
,ZFMISC_1
,XBOOLE_1
,SUBSET_1
,XREAL_1
,XBOOLE_0
,NAT_1
,XBOOLEAN
,XTUPLE_0
,FOMODEL0
,FUNCT_4
,XXREAL_2
,CARD_1
,ORDINAL1
,MEMBERED
,FINSET_1
;

requirements REAL, NUMERALS, SUBSET, BOOLE, ARITHM;
::}

begin :::preliminaries
:::the length of this section testifies for the effort put into
:::inducing the proven facts to the most common, low-level and 
:::desirably supported entities, rather than proving things about 
:::new and particular mathematical objects

definition func two equals 2; coherence; end;
registration 
cluster TRUE -> non empty; coherence; 
identify BOOLEAN with two; compatibility by CARD_1:50;
identify two with BOOLEAN; compatibility;
cluster non natural for number;
existence proof set n=the non zero Nat; take -n; thus thesis; end;
cluster boolean for non empty set; 
existence proof take TRUE; thus thesis; end;
cluster right_end non empty natural-membered for Subset of NAT;
existence
::{
proof 
reconsider IT=the right_end non empty natural-membered set as 
Subset of NAT by MEMBERED:6; take IT; thus thesis; 
end;
::}
let x be natural number; cluster {x}\NAT -> empty;
coherence by ORDINAL1:def 12, FOMODEL0:29;
reduce Segm x to x; reducibility; :::useless?
end;
registration let X be right_end ext-real-membered set;
cluster {max X}\X -> empty;
coherence 
proof max X in X by XXREAL_2:def 8; hence thesis by FOMODEL0:29; end;
end;

Lm0a: for m,n being Nat holds (m <= n iff m c= n)
proof let m,n be Nat; Segm m=m & Segm n=n; hence thesis by NAT_1:39; end;

reserve x,y,z,X,Y,i for set,P,Q,R for Relation,f,g for Function,v for Nat;

Lm9c: y<>{} implies (x=z iff ([:X,{{}}:]+*[:{z},{y}:]).x=y)
::{
proof
set f=[:X,{{}}:], gg=[:{z},{y}:], ff=f | (dom f\ dom gg), 
g=z.-->y, F=f +* gg, i=x, w=z; 
assume
B2: y<>{}; 
thus x=z implies F.x=y
proof
assume x=z; then i=w & F=f+*g; then F.i=y by FUNCT_4:113; hence thesis;
end;
assume F.i=y; then i in dom F & F.i=y by B2, FUNCT_1:def 2; then
B0: [i,y] in F by FUNCT_1:1; not y in {{}} by B2; then 
not [i,y] in f by ZFMISC_1:87; then not [i,y] in ff; then 
[i,y] in gg by B0, XBOOLE_0:def 3; then [i,y] in [:{w},{y}:]; then 
i in {w} by ZFMISC_1:87; hence thesis by TARSKI:def 1;
end;
::}

Lm9b: y<>{} implies (x=z iff ([:X,{{}}:]+*[:{z},{y}:]).x<>{})
::{
proof
set f=[:X,{{}}:], w=z, g=z.-->y, gg=[:{z},{y}:], 
ff=f | (dom f\ dom gg), F=f +* gg, i=x; assume y<>{}; hence 
x=z implies F.x<>{} by Lm9c; assume 
B2: F.x<>{}; assume x<>z; then not x in dom gg by TARSKI:def 1; 
then F.x=f.x by FUNCT_4:11 .= {}; hence contradiction by B2; 
end;
::}

Lm15: rng(P +*1 Q) c= rng P \/ rng Q & dom(P +*1 Q)=dom P \/ dom Q
::{
proof
set dP=dom P, dQ=dom Q, R=P +*1 Q, r=rng R, PP=P|(dP\dQ), rP=rng P, 
rQ=rng Q, dR=dom R, dPP=dom PP; rng PP \ rng P={}; then reconsider 
rPP=rng PP as Subset of rP by FOMODEL0:29; r=rPP \/ rQ by RELAT_1:12; 
hence r c= rP\/rQ by XBOOLE_1:9; dR=dPP\/dQ by XTUPLE_0:23 .= dP\/dQ 
by XBOOLE_1:39; hence thesis;
end;
::}

Lm13: f.x <> {} implies f.x in rng f
::{
proof
assume f.x<>{}; then x in dom f by FUNCT_1:def 2; hence thesis by FUNCT_1:3;
end;
::}

Lm17: f.x in {{}} \/ rng f 
::{
proof
set Y=rng f, y=f.x, e={{}}; y={} or y in Y by Lm13; 
then y in e or y in Y by TARSKI:def 1; hence thesis by XBOOLE_0:def 3;
end;
::}

Lm14: f is 2-valued implies f.x is boolean
::{
proof
set y=f.x,T={0,TRUE},Y=rng f,e={0}; e\T={}; then
B0: T=2 & e c= 2 by FOMODEL0:29; assume f is 2-valued; then 
reconsider Y,e as Subset of T by B0, RELAT_1:def 19; y in Y\/e 
by Lm17; then y in T; then y=0 or y=1 
by TARSKI:def 2; hence thesis by XBOOLEAN:def 3;
end;
::}

Lm12: for X being right_end Subset of NAT holds max X=union X
::{
proof
let X be right_end Subset of NAT; 
reconsider XX=X as right_end ext-real-membered set; 
set M=max XX, U=union XX;
now
let x; assume 
B0: x in XX; then reconsider n=x as Element of NAT; reconsider M as Nat;
n <= M by XXREAL_2:def 8, B0; then Segm n c= Segm M by NAT_1:39; hence 
x c= max XX;
end; then
B1: union X c= max X by ZFMISC_1:76;
M in XX by XXREAL_2:def 8; then max X c= union X by ZFMISC_1:74; 
hence thesis by XBOOLE_0:def 10, B1;
end;
::}

registration
let N be right_end Subset of NAT;
identify union N with max N;
compatibility by Lm12;
end;

registration
let X be non empty set, x be Element of X;
cluster {x}\X -> empty; coherence by FOMODEL0:29;
end;

registration let X,Y; let y be Subset of Y;
cluster X\Y \ (X\y) -> empty; 
coherence proof X null X is Subset of X; hence thesis; end;
end;

registration let X be non trivial set, x be trivial set;
cluster X\x -> non empty; coherence
proof X\x={} implies X c= x by FOMODEL0:29; hence thesis; end;
end;

registration
let Y be trivial non empty set; let X be non empty Subset of Y;
identify X null Y with Y null X;
compatibility
::{
proof
consider x being Element of X such that B0: {x}=X 
by SUBSET_1:46; Y={x} by ZFMISC_1:132; hence thesis by B0;
end;
::}
end;

registration let x be boolean non empty set; 
cluster x \+\ 1 -> empty;
coherence proof x=1 by XBOOLEAN:def 3; hence thesis by FOMODEL0:29; end;
end;

registration 
let x be non natural number;
cluster {x}\NAT -> non empty; coherence 
proof not x in NAT; hence thesis by FOMODEL0:29; end;
end;

registration let x; 
cluster union {x} \+\ x -> empty;
coherence by ZFMISC_1:25, FOMODEL0:29; let f be NAT-valued Function;
cluster f.x -> natural for number;
coherence
::{
proof
set y=f.x, Y=rng f; {0}\NAT={}; then reconsider e={0} as 
Subset of NAT by FOMODEL0:29; y in Y\/e & Y\/e c= NAT by Lm17; then 
y in NAT; then {y}\NAT={}; hence thesis;
end;
::}
end;

registration
let X,Y; let P be X-valued Relation, Q be Y-valued Relation;
cluster P +*1 Q -> (X\/Y)-valued for Relation;
coherence
::{
proof
set rP=rng P, rQ=rng Q, R=P +*1 Q, r=rng R; 
rP c= X & rQ c= Y & X null Y c= X\/Y & Y null X c= X\/Y; 
then reconsider rP, rQ as Subset of X\/Y by XBOOLE_1:1; 
r c= rP \/ rQ by Lm15; then r c= X\/Y by XBOOLE_1:1;
hence thesis by RELAT_1:def 19;
end;
::}
end;

registration let P,Q;
cluster dom (P +*1 Q) \+\ (dom P \/ dom Q) -> empty;
coherence by Lm15, FOMODEL0:29;
cluster rng (P +*1 Q) \(rng P \/ rng Q) -> empty;
coherence by Lm15, FOMODEL0:29;
end;

registration let x; let f be 2-valued Function;
cluster f.x -> boolean; coherence by Lm14;
end;

registration let P,Y;
cluster P"Y \ dom P -> empty;
coherence by RELAT_1:132, FOMODEL0:29;
end;

registration
let X3 be set; let X2 be Subset of X3; let X1 be Subset of X2;
cluster X1\X3 -> empty; coherence by XBOOLE_1:1, FOMODEL0:29;
end;
registration
let X; let x be Subset of X; let Y;
cluster x\(X\/Y) -> empty;
coherence proof X null Y c= X\/Y; hence thesis; end;
end;

notation let X; synonym natmax X for union X; end; 
:::as a reminder that, for a finite subset of NAT, set  
:::theoretical union is equivalent to taking the maximum 
:::(thanks to von Neumann encoding of ordinals)










begin :::definitions related to second price auctions, and basic facts

definition let R; 
func topbiddersof R -> Subset of dom R equals R"{natmax rng R};
coherence by RELAT_1:132; :::R"Y is the preimage of Y under R
end;

definition let R; func winnerof R equals 
the Element of topbiddersof R; coherence;
end;
definition let R; func losersof R -> Subset of dom R equals 
dom R \ {winnerof R}; coherence; end;
definition let R; func priceof R equals natmax rng (R | losersof R); 
coherence;
end;
:::priceof R, above, gives the second value, when the bids is a
:::finite set of natural numbers; the modification below preserves  
:::its simplicity while  patching the value to zero for bogus cases  
:::(e.g., non-numerical bids), saving typing-related work. This 
:::works because, by Mizar convention, the type "Element of {}" is 
:::inhabited by {} alone; and, in turn, 0={}, set-theoretically.
definition let R;:::R=bids; no need to require it is a sequence,yet 
func priceof R -> Element of NAT equals 
the Element of {priceof R} /\ NAT; ::/\ rng (R|losersof R)
coherence by ORDINAL1:def 12;
end;

Lm16b: rng (R|losersof R) c= NAT & rng (R|losersof R) is finite 
implies priceof R = natmax rng (R|losersof R)
proof
set L=losersof R, Q=R|L, rQ=rng Q, P1=natmax rQ, P2=priceof R; 
assume rQ c= NAT & rQ is finite; then reconsider rQ as finite Subset of NAT;
rQ={} or rQ is right_end Subset of NAT; then 
P1=0 or (rQ<>{} & P1=sup rQ) by Lm12; then P1 is natural;
then {P1} \ NAT={}; then 
reconsider X={P1} as non empty Subset of NAT by FOMODEL0:29;
X/\NAT=X null NAT; then P2 = the Element of X .= P1 by TARSKI:def 1;
hence thesis;
end;

Lm8: (rng R<>{} & rng R c= NAT & rng R is finite) 
implies winnerof R in topbiddersof R
::{
proof
set r=rng R, U=natmax r, w=winnerof R, O=topbiddersof R; assume 
r<>{} & r c= NAT; then 
reconsider XX=r as non empty real-membered Subset of NAT; 
assume r is finite; then reconsider X=XX as right_end Subset of NAT; 
max X=U by Lm12; then U in X by XXREAL_2:def 8; then r meets {U} 
by ZFMISC_1:48; then O<>{} by RELAT_1:138; hence thesis;
end;
::}

definition let R;
func R-pay ->Function equals [:dom R,{0}:]+*[:{winnerof R},{priceof R}:];
coherence; :::R-pay is how much each bidder must pay in the end
func R-allocations ->Function equals [:dom R,{0}:]+*[:{winnerof R},{1}:];
coherence; :::how much of the sold asset each bidder gets in the end
end;

registration 
let R; cluster R-allocations -> 2-valued for Function; 
coherence :::2-valued: we treat only the case of an indivisible asset
proof R-allocations is BOOLEAN-valued; hence thesis; end;
let x; cluster R-pay.x -> natural for number; 
coherence :::The case of integer currency is much simpler to formalize
::{
proof
set P=priceof R, A={0}, B={P}; A\NAT={} & B\NAT={}; then 
A c= NAT & B c= NAT by FOMODEL0:29; hence thesis;
end;
::}
end;:::fractional currency'd be overkill and not realistic 








begin :::the core theorems

Lm3: R-pay.(winnerof R) = priceof R
::{
proof
set w=winnerof R, P=priceof R, g=[:{w},{P}:];
g=w.-->P; hence thesis by FUNCT_4:113;
end;
::}

Lm0: natmax rng (P | (dom P \ dom Q)) c< natmax rng Q 
implies topbiddersof (P +*1 Q)=Q"{natmax rng Q} 
:::c< is strict set inclusion; thanks to von Neumann encoding 
:::(see above comments), this translates to <
::{
proof set dP=dom P, 
dQ=dom Q, PP=P|(dP\dQ), R=P +*1 Q, maxQ=natmax rng Q; assume
B0: natmax rng PP c< maxQ; then reconsider 
maxPP=natmax rng PP as Subset of maxQ by XBOOLE_0:def 8;
B1: topbiddersof R=R"{natmax (rng PP \/ rng Q)} by RELAT_1:12 .= 
(PP \/ Q)"{maxQ null maxPP} by ZFMISC_1:78 .= PP"{maxQ} \/ Q"{maxQ} 
by FOMODEL0:23; not maxQ c= maxPP by B0, XBOOLE_0:def 8; then not 
maxQ in rng PP by ZFMISC_1:74; then {maxQ} misses rng PP 
by ZFMISC_1:50; then PP"{maxQ}={} by RELAT_1:138; hence thesis by B1; 
end;
::}

Lm1: {union X}\X={} & natmax rng (P | (dom P\{x})) c< union X  
implies topbiddersof (P +*1 [:{x},X:])={x}
::{
proof
assume 
B2: {union X}\X={}; then reconsider XX=X as non empty set;
reconsider UX=union XX as Element of XX by B2, FOMODEL0:29; {UX}\XX={}; 
then reconsider UUX={UX} as non empty Subset of XX by FOMODEL0:29;
set dP=dom P, rP=rng P, maxP=union rP, Q=[:{x},XX:], dQ=dom Q, rQ=rng Q, 
PP1=P|(dP\{x}), PP=P|(dP\dQ), maxQ=union rQ, R=P +*1 Q, QQ=[:XX,{x}:];
:: B0: rQ=X & dom QQ=X; 
UUX /\ XX= UUX null XX; then 
B3: {UX} meets rQ by XBOOLE_0:def 7;
assume natmax rng PP1 c< union X; then 
natmax rng PP c< maxQ; then 
B1: topbiddersof R=Q"{maxQ} by Lm0 .= Q"{UX}; reconsider 
Y={UX} as Subset of rQ by FOMODEL0:29, B2; Q"{UX} \ dom Q={}; 
then reconsider T=topbiddersof R as Subset of {x} by 
FOMODEL0:29, B1; Q"{UX}<>{} by B3, RELAT_1:138; then reconsider 
T as non empty Subset of {x} by B1; T null {x}={x}; hence thesis; 
end;
::}

Lm5: R-pay.i c< union Y & rng (R|losersof R) c= NAT & 
rng (R|losersof R) is finite & i=winnerof R & {union Y}\Y={}
implies 
((R +*1 [:{i},Y:])-pay.i = R-pay.i & 
winnerof (R +*1 [:{i},Y:]) = winnerof R)
::{
proof
assume 
B1: R-pay.i c< union Y; then union Y <> {} by XBOOLE_1:62; then
reconsider YY=Y as non empty set;
set y=union YY;
set B=R, x=B-allocations, p=B-pay, P=p.i, f=[:{i},YY:], L=losersof R, 
Bi=B +*1 f, pii=Bi-pay, Pi=pii.i, w=winnerof B, wi=winnerof Bi, 
D=dom B, Di=dom Bi, l=losersof R, RR=B|l; D\/dom f \+\ Di={}; then 
D \/ dom f=Di & dom f \+\ {i}={} by FOMODEL0:29; then 
dom f={i} & Di=D\/{i}; then
B2: Di\{i} = D\{i} by XBOOLE_1:40; (D\{i}) /\ ({i}/\{i})={}; then 
B4: D\{i} misses dom f by XBOOLE_0:def 7;
:: B7: i=w & rng B c= NAT;
assume 
:: priceof B<>0 
B7: rng (R|L) c= NAT & rng (R|L) is finite  
& i=w; then P=priceof B by Lm3; then 
B8: P=natmax rng RR by B7, Lm16b;
assume {union Y}\Y={}; then 
topbiddersof Bi={i} by Lm1, B8, B7, B1; then 
wi in topbiddersof Bi & topbiddersof Bi={i}; then 
B0: wi=i & i=w by TARSKI:def 1, B7; then
Bi|(Di\{wi}) = (B|(D\{i})|(D\{i})) \/ (f|(D\{i})) 
by B2, FOMODEL0:56 .= (B | (D\{i})) \/ {} by B4, RELAT_1:66 .= 
B|(D\{w}) by B0; then priceof B=Pi by Lm3, B0; hence 
P=(B +*1 [:{i},Y:])-pay.i by Lm3, B0; thus thesis by B0;
end;
::}

Lm4: rng R is finite Subset of NAT & 
Y is non empty Subset of NAT & union Y c= v 
implies 
(R +*1 [:{i},Y:])-pay.i <= (R +*1 [:{i},Y:])-allocations.i*v
::{
proof
set Q=[:{i},Y:], Ri=R +*1 Q, pi=Ri-pay, xi=Ri-allocations, rQ=rng Q, 
wi=winnerof Ri, LH=pi.i, RH=v*(xi.i), P=priceof R, Pi=priceof Ri
, ri=rng Ri; assume rng R is finite Subset of NAT; then reconsider 
rR=rng R as finite Subset of NAT; assume Y is non empty Subset of NAT;
then reconsider YY=Y as non empty Subset of NAT; assume 
B1: union Y c= v; then reconsider YY as non empty 
finite ext-real-membered Subset of NAT by FINSET_1:7; 
rQ \+\ Y={} & ri \ (rR \/ rQ) = {}; then ri c= rR \/ YY by FOMODEL0:29;
then reconsider ri as finite non empty Subset of NAT by XBOOLE_1:1;
per cases;
suppose
C0: i=wi; then xi.i=1 & LH=Pi by Lm3, Lm9c; then 
C2: RH=v & xi.i=1 & LH=Pi; ri is finite; then
wi in topbiddersof Ri by Lm8; then consider y being element such that
C4: [wi,y] in Ri & y in {union ri} by RELAT_1:def 14;
dom [:{i},YY:] \+\ {i}={}; then i in dom Q by TARSKI:def 1;
then not i in dom R \ dom Q by XBOOLE_0:def 5; then 
C5: not [i,union ri] in R|(dom R \ dom Q) by RELAT_1:def 11; 
[i,union ri] in Ri by C0, C4, TARSKI:def 1; then
[i,union ri] in Q by C5, XBOOLE_0:def 3; then  
C3: union ri in YY by ZFMISC_1:87; then 
reconsider middle=union ri as Element of NAT;
middle <= max YY by C3, XXREAL_2:def 8; then 
middle c= max YY & max YY c= v by Lm0a, Lm12, B1;
then middle c= v by XBOOLE_1:1; then
reconsider middle as Subset of v;
rng (Ri|losersof Ri) \ ri = {} & ri is finite; then 
C6: rng (Ri|losersof Ri) is finite Subset of ri by FOMODEL0:29; then
Pi=union rng (Ri|losersof Ri) by Lm16b, XBOOLE_1:1; then 
reconsider Pi as Subset of middle by ZFMISC_1:77, C6; 
(Pi /\ middle)\(middle \/ v)={}; then
Pi null middle c= v null middle; then 
Segm priceof Ri c= Segm v; then priceof Ri <= v by NAT_1:39; 
hence thesis by C2;
end;
suppose
i<>wi; then i<>wi & (priceof Ri<>0 or priceof Ri=0); 
then LH=0 by Lm9b; hence thesis;
end;
end;
::}

Lm21: :::generalized Vickrey's theorem: 
::: multiple (distinct) bids per participant and zero-valued bids
for V being non empty finite Subset of NAT st  
rng R is finite Subset of NAT & union V = v holds
R-allocations.i*v - (R-pay.i) <= 
(R +*1 [:{i},V:])-allocations.i*v - (R +*1 [:{i},V:])-pay.i
::{
proof
let V be non empty finite Subset of NAT; set x=R-allocations, 
p=R-pay, g=[:{i},V:], Ri=R +*1 g; reconsider g as NAT-valued Relation; 
assume
B11: rng R is finite Subset of NAT; then
rng Ri \ (rng R \/ rng g)={} & rng R \/ rng g \ NAT={}; then
rng Ri c= rng R \/ rng g & rng R \/ rng g c= NAT by FOMODEL0:29; then
reconsider Ri=R +*1 g as NAT-valued Relation by XBOOLE_1:1, RELAT_1:def 19;
set pi=Ri-pay, xi=Ri-allocations, LH=v*(x.i)-(p.i), P=priceof R, 
L=losersof R, RH=v*(xi.i) - pi.i, w=winnerof R, wi=winnerof Ri, 
Pi=priceof Ri;
B10: rng (R|L) \ rng R = {} & xi.wi=1 by Lm9c; assume 
B12: union V = v; then pi.i-(pi.i)<=RH & rng (R|L) c= rng R 
by XREAL_1:9, Lm4, FOMODEL0:29, B10, B11; then 
B0: 0<=RH & rng (R|L) is finite by B11; rng (R|L)\rng R={}; then 
rng (R|L) c= rng R by FOMODEL0:29; then
B9: rng (R|L) c= NAT & rng (R|L) is finite 
& {union V}\V={} by B11, XBOOLE_1:1;
per cases;
suppose LH <= 0; then LH <= 0 & RH >= 0 by B0; hence thesis; end;
suppose 
C1: LH > 0; then x.i <> {}; then x.i \+\ 1={}; then 
C0: x.i=1 by FOMODEL0:29; then LH=v-(p.i); then 
v-p.i > (p.i)-(p.i) & x.i<>{} by C1; then v > p.i by XREAL_1:9; 
then Segm p.i c= Segm v & p.i <> v by NAT_1:39; then 
C3: p.i c< v & i=w by C0, Lm9c, XBOOLE_0:def 8; then pi.i=p.i & w=wi 
by Lm5, B9, B12; then pi.i=p.i & i=wi by C3; then 
pi.i=p.i & xi.i=1 by B10; then LH=RH by C0; hence thesis;
end;
end;
::}

::$EOF

:: Lm21: :::Vickrey's theorem, version 1
:: f is NAT-valued & rng f is finite & rng (f|losersof f) c= NAT implies 
:: (v*(f-allocations.i)) - ((f-pay).i) <= 
:: v*((f +* [:{i},{v}:])-allocations).i - (f +* [:{i},{v}:])-pay.i
:: ::{
:: proof
:: assume f is NAT-valued; then reconsider R=f as NAT-valued Function; 
:: set x=R-allocations, V={v}, p=R-pay, g=[:{i},V:]; V\NAT={}; then 
:: V c= NAT by FOMODEL0:29; then reconsider g as NAT-valued Function;
:: reconsider Ri=R +*1 g as NAT-valued Function;
:: set pi=Ri-pay, xi=Ri-allocations, LH=v*(x.i)-(p.i), P=priceof R, L=losersof f, 
:: RH=v*(xi.i) - pi.i, w=winnerof R, wi=winnerof Ri, Pi=priceof Ri;
:: B10: rng (R|L) \ rng R ={} & xi.wi \+\ 1={}; assume 
:: B11: rng f is finite; then pi.i-(pi.i)<=RH
:: & rng (f|L) c= rng R by XREAL_1:9, Lm4, FOMODEL0:29, B10; then 
:: B0: 0<=RH & rng (f|L) is finite by B11; assume 
:: B9: rng (f|L) c= NAT;
:: per cases;
:: suppose LH <= 0; then LH <= 0 & RH >= 0 by B0; hence thesis;
:: end;
:: suppose 
:: C1: LH > 0; then x.i <> {}; then x.i \+\ 1={}; then 
:: C0: x.i=1 by FOMODEL0:29; then LH=v-(p.i); then v-p.i > (p.i)-(p.i) & 
:: x.i<>{} by C1; then v > p.i by XREAL_1:9; then Segm p.i c= Segm v & 
:: p.i <> v by NAT_1:39; then 
:: C3: p.i c< v & i=w by C0, Lm9c, 
:: XBOOLE_0:def 8; then pi.i=p.i & p.i=priceof R & w=wi by Lm5, Lm3, B9, B0; 
:: then 
:: C2: pi.i=p.i & i=wi by C3, B9;
:: then pi.i=p.i & xi.i=1 by B10, FOMODEL0:29; then
:: :: then 
:: :: i=wi by Lm9b; then xi.i<>{} by Lm9b; 
:: :: then xi.i \+\ 1={}; then pi.i=p.i & xi.i=1 
:: :: by C2, FOMODEL0:29; then 
:: LH=RH by C0; hence thesis;
:: end;
:: end;
:: ::}

:: Lm20: (rng R c= NAT & rng R is finite & dom R is non trivial) 
:: implies priceof R in rng R
:: ::{
:: proof
:: set P=priceof R, p=R-pay, r=rng R, rp=rng p, l=losersof R, 
:: D=dom R, RR=R|l, rr=rng RR; assume r c= NAT & r is finite; 
:: then reconsider r as finite Subset of NAT; rr \ r={}; then 
:: B1: rr c= r & r c= NAT by FOMODEL0:29; then reconsider 
:: rr as finite Subset of NAT by XBOOLE_1:1;
:: set PP=natmax rr; assume D is non trivial; then 
:: l<>{} & l /\ D=l null D; then l meets D by XBOOLE_0:def 7; then 
:: RR is non empty by RELAT_1:66; then rr<>{}; then reconsider 
:: rr as right_end Subset of NAT; max rr=PP by Lm12; then 
:: B0: PP in rr by XXREAL_2:def 8; then PP in NAT; 
:: then {PP}\NAT={}; then reconsider X={PP} as Subset of NAT 
:: by FOMODEL0:29; X/\NAT=X null NAT; then P in {PP}; then P=PP 
:: by TARSKI:def 1; hence thesis by B0, B1;
:: end;
:: ::}

:: Lm22: rng f c= NAT\{0} & rng f is finite & dom f is non trivial   
:: implies :::Vickrey's theorem, version 2
:: (v*(f-allocations.i)) - ((f-pay).i) <= 
:: v*((f +* [:{i},{v}:])-allocations).i - (f +* [:{i},{v}:])-pay.i
:: ::{
:: proof
:: set Y=rng f, X=dom f, p=priceof f; assume 
:: B1: Y c= NAT\{0}; then 
:: B0: Y c= NAT by XBOOLE_1:1; assume 
:: B2: Y is finite & X is non trivial; 
:: then p in Y by Lm20, B0; then p in NAT\{0} by B1; then 
:: not p in {0} by XBOOLE_0:def 5; then
:: p<>0 by TARSKI:def 1; then f is NAT-valued & Y is finite & p<>0 
:: by RELAT_1:def 19, B0, B2; hence thesis by Lm21;
:: end;
:: ::}

:: Lm7: winnerof f in topbiddersof f implies 
:: (f.(winnerof f)=union rng f & priceof f c= f.(winnerof f))
:: ::{
:: proof
:: set w=winnerof f, P=priceof f, I=topbiddersof f, y=f.w, l=losersof f; 
:: reconsider r=rng (f|l) as Subset of rng f by RELAT_1:70; 
:: assume w in I; then consider z being element such that
:: B0: [w,z] in f & z in {natmax rng f} by RELAT_1:def 14; {}\f.w={}; then 
:: B1: {} c= f.w & w in dom f & z=f.w by FOMODEL0:29, FUNCT_1:1, B0; 
:: hence f.w=natmax rng f by TARSKI:def 1, B0; then 
:: union r c= f.w by ZFMISC_1:77; hence thesis by Lm16, B1;
:: end;
:: ::}

:: Lm16: priceof R=0 or priceof R=natmax rng (R|losersof R)
:: ::{
:: proof
:: set P=priceof R, l=losersof R, RR=R|l, Y=rng RR, PP=natmax Y; 
:: assume P<>0; then {PP}/\NAT <> {} by SUBSET_1:def 1; then P in {PP}/\NAT;
:: then P in {PP}; hence thesis by TARSKI:def 1;
:: end;
:: ::}

:: registration
:: let N be non empty finite Subset of NAT;
:: cluster sup N -> natural;
:: coherence;
:: end;

:: cluster P"{Y\rng P} -> empty;
:: coherence;

